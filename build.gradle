// Top-level build file where you can add configuration options common to all sub-projects/modules.

// Load dependencies
apply from: 'base/dependencies.gradle'

buildscript {
    ext.kotlin_version = '1.2.51'

    //Dependencies defined here will be common for all modules added in this project
    ext.sharedGroup = {dependencyHandler->
        delegate = dependencyHandler

        implementation fileTree(dir: 'libs', include: ['*.jar'])
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

        //Support Libraries
        implementation supportDependencies.appCompat
        implementation supportDependencies.constraint
        implementation supportDependencies.lifeCycle

        //Dagger
        implementation daggerDependencies.dagger
        implementation daggerDependencies.daggerSupport

        //kapt
        kapt daggerDependencies.daggerCompiler
        kapt daggerDependencies.daggerProcessor

        //Rx
        implementation reactiveDependencies.values()

        //RestClient
        implementation retrofitDependencies.values()

        //Logging
        implementation timber

        //Test Libraries
        testImplementation testDependencies.junit
        androidTestImplementation testDependencies.testRunner
        androidTestImplementation testDependencies.espressoCore
    }

    repositories {
        jcenter()
        maven { url 'https://maven.google.com' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.0-alpha04'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.realm:realm-gradle-plugin:5.2.0"
    }
}


allprojects {
    repositories {
        jcenter()
        maven { url 'https://maven.google.com' }
        maven { url 'https://jitpack.io' }
    }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}